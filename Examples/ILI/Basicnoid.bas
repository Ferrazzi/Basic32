10 TIMERSPLASH = 0
20 ILI INIT 17 16 5 3
30 ILI LED 32 1
40 LBTN = 27
50 RBTN = 25
60 PINMODE LBTN INPUT PULLUP
70 PINMODE RBTN INPUT PULLUP
80 REM ==== SPLASH SCREEN ====
90 ILI SETBGCOLOR 0 0 0
100 ILI CLEAR
110 REM ==== DENTER SPLASH ====
120 T$ = "BASICNOID"
130 SZ = 2
140 TW = LEN(T$) * 6 * SZ
150 TH = 8 * SZ
160 TX = INT((320 - TW) / 2)
170 TY = INT(120 - TH - 10)
180 ILI TEXT TX TY SZ T$ 255 0 0
190 T2$ = "Press a button to start"
200 SZ2 = 1
210 TW2 = LEN(T2$) * 6 * SZ2
220 TH2 = 8 * SZ2
230 TX2 = INT((320 - TW2) / 2)
240 TY2 = INT(120 + TH + 5)
250 ILI TEXT TX2 TY2 SZ2 T2$ 180 180 180
260 REM ==== WAIT FOR BUTTON PRESS ====
270 LB = DLEVEL(LBTN)
280 RB = DLEVEL(RBTN)
290 TIMERSPLASH = TIMERSPLASH + 1
300 IF TIMERSPLASH > 160 THEN TIMERSPLASH= 0
310 IF TIMERSPLASH < 80 THEN ILI TEXT TX2 TY2 SZ2 T2$ 180 180 180
320 IF TIMERSPLASH > 80 THEN ILI TEXT TX2 TY2 SZ2 T2$ 0 0 0
330 IF LB <> 0 AND RB <> 0 THEN GOTO 270
340 REM ==== PLAYFIELD SETUP ====
350 W = 240
360 H = 240
370 ILI SETBGCOLOR 0 0 0
380 ILI CLEAR
390 REM PADDLE ID=1
400 PADW = 50
410 PADH = 8
420 PADX = 95
430 PADY = 220
440 PADSPD = 6
450 PAD_OLDX = PADX
460 BALLSPD = 2
470 PMOVED = 1
480 PHIT = 0
490 ILI SPRITE NEW 1 RECT PADX PADY PADW PADH 255 255 255
500 REM PALLA ID=2
510 BR = 5
520 BX = 120
530 BY = 120
540 ILI SPRITE NEW 2 CIRCLE BX BY BR 255 255 255
550 DX = 2
560 DY = -2
570 REM ==== BRICKS ====
580 COLS = 8
590 ROWS = 4
600 BW = 24
610 BH = 12
620 SPX = 4
630 SPY = 4
640 SX = 8
650 SY = 30
660 MAXBRICKS = COLS * ROWS
670 DIM ALIVE(MAXBRICKS)
680 DIM BRID(MAXBRICKS)
690 TOTBRICKS = 0
700 CELLW = BW + SPX
710 CELLH = BH + SPY
720 REM ==== RIGHT SIDE BORDER ====
730 ILI RECT W 0 2 H 150 150 150
740 SID = 3
750 REM ==== LEVEL PATTERN ARRAY ====
760 DIM PAT$(3)
770 GOSUB 2390
780 FOR RY = 0 TO ROWS - 1
790 FOR CX = 0 TO COLS - 1
800 XX = SX + CX * CELLW
810 YY = SY + RY * CELLH
820 I = RY * COLS + CX
830 REM ==== PATTERN CHARACTER FOR THIS CELL ====
840 C$ = MID$(PAT$(RY), CX + 1, 1)
850 REM ==== IF IT'S EMPTY (.) DO NOT CREATE THE BRICK ====
860 IF C$ = "." THEN ALIVE(I) = 0 : BRID(I) = 0 : GOTO 1030
870 REM ==== MAP CHARACTER → COLOR ====
880 IF C$ = "R" THEN CR = 255 : CG = 80 : CB = 80 ' RED
890 IF C$ = "V" THEN CR = 0 : CG = 255 : CB = 0 ' GREEN
900 IF C$ = "B" THEN CR = 0 : CG = 0 : CB = 255 ' BLUE
910 IF C$ = "G" THEN CR = 255 : CG = 255 : CB = 0 ' YELLOW
920 IF C$ = "W" THEN CR = 255 : CG = 255 : CB = 255 ' WHITE
930 IF C$ = "A" THEN CR = 160 : CG = 160 : CB = 160 ' GREY
940 IF C$ = "P" THEN CR = 180 : CG = 0 : CB = 255 ' PURPLE
950 IF C$ = "C" THEN CR = 0 : CG = 200 : CB = 255 ' LIGHT BLUE
960 IF C$ = "O" THEN CR = 255 : CG = 160 : CB = 0 ' ORANGE
970 IF C$ = "M" THEN CR = 160 : CG = 80 : CB = 0 ' BRAWN
980 ILI SPRITE NEW SID RECT XX YY BW BH CR CG CB
990 ALIVE(I) = 1
1000 BRID(I) = SID
1010 TOTBRICKS = TOTBRICKS + 1
1020 SID = SID + 1
1030 NEXT CX
1040 NEXT RY
1050 REM ==== DRAW EVERYTHING ONLY ONCE ====
1060 ILI SPRITE DRAW
1070 REM ==== GAME STATE ====
1080 ROUND = 1
1090 LIVES = 3
1100 SCORE = 0
1110 GAME = 1
1120 GOSUB 2020
1130 REM ===== GAME LOOP =====
1140 IF GAME = 0 THEN GOTO 2370
1150 REM ==== INPUT PADDLE ====
1160 LB = DLEVEL(LBTN)
1170 RB = DLEVEL(RBTN)
1180 IF LB = 0 THEN PADX = PADX - PADSPD
1190 IF RB = 0 THEN PADX = PADX + PADSPD
1200 IF PADX < 0 THEN PADX = 0
1210 IF PADX > (W - PADW) THEN PADX = W - PADW
1220 IF PADX <> PAD_OLDX THEN PMOVED = 1
1230 IF PMOVED = 1 THEN ILI SPRITE MOVE 1 PADX PADY
1240 PAD_OLDX = PADX
1250 REM ==== PHYSICS MICRO-STEPS FOR THE BALL ====
1260 FOR S = 1 TO BALLSPD
1270 BX = BX + DX
1280 BY = BY + DY
1290 IF BX <= BR THEN DX = ABS(DX)
1300 IF BX >= (W - BR) THEN DX = -ABS(DX)
1310 IF BY <= BR THEN DY = ABS(DY)
1320 REM ==== PADDLE COLLISION WITH SIMPLE EFFECT ====
1330 REM ==== CONSIDER THE PADDLE ONLY IF THE BALL IS MOVING DOWNWARD ====
1340 IF DY < 0 THEN GOTO 1520
1350 IF BY < (PADY - BR) THEN GOTO 1520
1360 IF BX < PADX THEN GOTO 1520
1370 IF BX > (PADX + PADW) THEN GOTO 1520
1380 REM ==== IMPACT POINT RELATIVE TO THE PADDLE CENTER ====
1390 HIT = BX - (PADX + PADW / 2)
1400 ZONE = PADW / 5
1410 REM ==== START FROM THE MOST EXTREME CASE ON THE LEFT ====
1420 DX = -3
1430 IF HIT > -2 * ZONE THEN DX = -2
1440 IF HIT > -ZONE THEN DX = 1
1450 IF HIT > ZONE THEN DX = 2
1460 IF HIT > 2 * ZONE THEN DX = 3
1470 REM ==== BOUNCE UPWARD, PRESERVING THE DY MAGNITUDE ====
1480 DY = -ABS(DY)
1490 REM ==== PLACE THE BALL SLIGHTLY ABOVE THE PADDLE ====
1500 BY = PADY - BR - 1
1510 PHIT = 1
1520 REM ==== BRICK COLLISION ====
1530 CX = INT((BX - SX) / CELLW)
1540 RY = INT((BY - SY) / CELLH)
1550 IF CX < 0 THEN GOTO 1810
1560 IF CX >= COLS THEN GOTO 1810
1570 IF RY < 0 THEN GOTO 1810
1580 IF RY >= ROWS THEN GOTO 1810
1590 I = RY * COLS + CX
1600 IF ALIVE(I) = 0 THEN GOTO 1810
1610 XX = SX + CX * CELLW
1620 YY = SY + RY * CELLH
1630 IF BX + BR < XX THEN GOTO 1810
1640 IF BX - BR > XX + BW THEN GOTO 1810
1650 IF BY + BR < YY THEN GOTO 1810
1660 IF BY - BR > YY + BH THEN GOTO 1810
1670 ALIVE(I) = 0
1680 SCORE = SCORE + 10 : GOSUB 2100
1690 TOTBRICKS = TOTBRICKS - 1
1700 IDHIT = BRID(I)
1710 ILI SPRITE DELETE IDHIT
1720 P1 = (XX + BW) - (BX - BR)
1730 P2 = (BX + BR) - XX
1740 P3 = (YY + BH) - (BY - BR)
1750 P4 = (BY + BR) - YY
1760 PXX = P1
1770 IF P2 < PXX THEN PXX = P2
1780 PYY = P3
1790 IF P4 < PYY THEN PYY = P4
1800 IF PXX < PYY THEN DX = -DX ELSE DY = -DY
1810 NEXT S
1820 REM ==== EXIT THROUGH THE BOTTOM → LIFE LOST AND RESET ====
1830 IF BY <= H THEN GOTO 1910
1840 LIVES = LIVES - 1 : GOSUB 2140
1850 IF LIVES = 0 THEN GAME = 0
1860 IF GAME = 0 THEN GOTO 2180
1870 BX = 120
1880 BY = 120
1890 DX = 2
1900 DY = -2
1910 IF TOTBRICKS = 0 THEN GAME = 0
1920 IF GAME = 0 THEN GOTO 2280
1930 REM ==== DRAW ONLY THE SPRITES THAT MOVE ====
1940 ILI SPRITE MOVE 2 BX BY
1950 IF PMOVED = 1 OR PHIT = 1 THEN ILI SPRITE DRAW 1
1960 ILI SPRITE DRAW 2
1970 ILI RECT W 0 2 H 150 150 150
1980 PMOVED = 0
1990 PHIT = 0
2000 DELAY 16
2010 GOTO 1130
2020 REM ==== SIMPLE HUD: SCORE + LIVES ONLY ====
2030 ILI TEXT 245 10 1 "SCORE" 255 255 255
2040 ILI TEXT 245 40 1 "LIVES" 255 255 255
2050 ILI SPRITE NEW 200 NUM 245 20 SCORE 1 255 255 0
2060 ILI SPRITE NEW 201 NUM 245 50 LIVES 1 0 255 0
2070 ILI SPRITE DRAW 200
2080 ILI SPRITE DRAW 201
2090 RETURN
2100 REM ==== UPDATE SCORE ====
2110 ILI SPRITE SETNUM 200 SCORE 255 255 0
2120 ILI SPRITE DRAW 200
2130 RETURN
2140 REM ==== UPDATE LIVES ====
2150 ILI SPRITE SETNUM 201 LIVES 0 255 0
2160 ILI SPRITE DRAW 201
2170 RETURN
2180 REM ==== GAME OVER ====
2190 T$ = "GAME OVER"
2200 SZ = 2
2210 TW = LEN(T$) * 6 * SZ
2220 TH = 8 * SZ
2230 TX = INT((W - TW) / 2)
2240 TY = INT((H - TH) / 2)
2250 ILI TEXT TX TY SZ T$ 255 0 0
2260 DELAY 3000
2270 GOTO 80
2280 REM ==== YOU WIN ====
2290 T$ = "YOU WIN!"
2300 SZ = 2
2310 TW = LEN(T$) * 6 * SZ
2320 TH = 8 * SZ
2330 TX = INT((W - TW) / 2)
2340 TY = INT((H - TH) / 2)
2350 ILI TEXT TX TY SZ T$ 0 255 0
2360 GOTO 2370
2370 REM ==== END ====
2380 END
2390 REM ==== LEVEL PATTERN DEFINITION (ROUND=1) ====
2400 PAT$(0) = "RR..VV.."
2410 PAT$(1) = "BBPPCCOO"
2420 PAT$(2) = "MMGGWWAA"
2430 PAT$(3) = "RRBBGGVV"
2440 RETURN
