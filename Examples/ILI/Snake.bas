10 REM ===== SNAKE ESP32 DISPLAY 320X240 =====
20 ILI INIT 17 16 5 3
30 ILI LED 32 1
35 SPEED = 120
40 REM ==== BUTTON CONFIGURATION ====
50 LBTN = 27
60 RBTN = 25
70 UBTN = 12
80 DBTN = 26
90 PINMODE LBTN INPUT PULLUP DEBOUNCE 60
100 PINMODE RBTN INPUT PULLUP DEBOUNCE 60
110 PINMODE UBTN INPUT PULLUP DEBOUNCE 60
120 PINMODE DBTN INPUT PULLUP DEBOUNCE 60
130 REM ==== SPLASH SCREEN ====
140 ILI SETBGCOLOR 0 0 0
150 ILI CLEAR
160 TITLE$ = "SNAKE Basic32"
170 MSG$ = "Press a key to start"
180 REM ==== CENTERED POSITION CALCULATION FOR THE TITLE (FONT 3 â‰ˆ 18 PX PER CHARACTER) ====
190 REM ==== WAIT FOR BUTTON PRESS (PULL-UP: PRESSED = 0) ====
191 TW = LEN(TITLE$) * 18
192 TX = (320 - TW) / 2
193 ILI TEXT TX 70 3 TITLE$ 0 255 0
194 MW = LEN(MSG$) * 6
195 MX = (320 - MW) / 2
196 ILI TEXT MX 130 1 MSG$ 200 200 200
200 LB = DLEVEL(LBTN)
210 RB = DLEVEL(RBTN)
220 UB = DLEVEL(UBTN)
230 DB = DLEVEL(DBTN)
240 IF LB=1 AND RB=1 AND UB=1 AND DB=1 THEN GOTO 200
250 REM ==== PLAYFIELD SETTINGS ====
260 CELL = 12
270 SCRW = 320
280 SCRH = 240
290 GRIDW = 26
300 GRIDH = 18
310 OFFX = 4
320 OFFY = 20
330 REM ==== CLEAR SCREEN AND DRAW HUD ====
340 ILI SETBGCOLOR 0 0 0
350 ILI CLEAR
360 REM ==== HUD SCORE ====
370 ILI TEXT 4 0 2 "SCORE" 255 255 255
380 SCORE = 0
390 ID_SCORE = 250
400 ILI SPRITE NEW ID_SCORE NUM 100 0 SCORE 2 255 255 0
410 ILI SPRITE DRAW ID_SCORE
420 REM ==== OUTER BORDER OF THE PLAYFIELD ====
430 BW = GRIDW * CELL
440 BH = GRIDH * CELL
450 BX = OFFX - 2
460 BY = OFFY - 2
470 ILI RECT BX BY BW+4 BH+4 100 100 100
480 REM ==== SNAKE DATA ====
490 MAXLEN = 100
500 DIM SX(MAXLEN)
510 DIM SY(MAXLEN)
520 REM ==== INITIAL SNAKE POSITION ====
530 SLEN = 3
540 HX = INT(GRIDW / 2)
550 HY = INT(GRIDH / 2)
560 SX(0) = HX
570 SY(0) = HY
580 SX(1) = HX - 1
590 SY(1) = HY
600 SX(2) = HX - 2
610 SY(2) = HY
620 REM ==== INITIAL DIRECTION ====
630 DX = 1
640 DY = 0
650 REM ==== PRE-CREATION OF ALL SNAKE SPRITES ====
660 FOR I = 0 TO MAXLEN - 1
670 ID = I + 1
680 XPIX = -CELL
690 YPIX = -CELL
700 IF ID = 1 THEN ILI SPRITE NEW ID RECT XPIX YPIX CELL CELL 0 255 0 ELSE ILI SPRITE NEW ID RECT XPIX YPIX CELL CELL 0 200 0
710 NEXT I
720 REM ==== PLACE ONLY THE FIRST SLEN SEGMENTS IN THE INITIAL CELLS ====
730 FOR I = 0 TO SLEN - 1
740 ID = I + 1
750 XPIX = OFFX + SX(I) * CELL
760 YPIX = OFFY + SY(I) * CELL
770 ILI SPRITE MOVE ID XPIX YPIX
780 ILI SPRITE DRAW ID
790 NEXT I
800 REM ==== FOOD ====
810 FX = 0
820 FY = 0
830 FOODNEW = 1
840 GOSUB 9000
850 REM ==== GAME STATE ====
860 GAME = 1
870 REM ==== INITIAL LOOP ====
880 IF GAME = 0 THEN GOTO 2000
890 REM ==== READ BUTTONS ====
900 LB = DLEVEL(LBTN)
910 RB = DLEVEL(RBTN)
920 UB = DLEVEL(UBTN)
930 DB = DLEVEL(DBTN)
940 REM ==== CHANGE DIRECTION ====
950 IF LB = 0 AND DX <> 1 THEN DX = -1 : DY = 0
960 IF RB = 0 AND DX <> -1 THEN DX = 1 : DY = 0
970 IF UB = 0 AND DY <> 1 THEN DX = 0 : DY = -1
980 IF DB = 0 AND DY <> -1 THEN DX = 0 : DY = 1
990 REM ==== CALCULATE NEW HEAD POSITION ====
1000 NHX = SX(0) + DX
1010 NHY = SY(0) + DY
1020 REM ==== WALL COLLISION ====
1030 IF NHX < 0 THEN GAME = 0 : GOTO 2000
1040 IF NHX >= GRIDW THEN GAME = 0 : GOTO 2000
1050 IF NHY < 0 THEN GAME = 0 : GOTO 2000
1060 IF NHY >= GRIDH THEN GAME = 0 : GOTO 2000
1070 REM ==== BODY COLLISION ====
1080 FOR I = 0 TO SLEN - 1
1090 IF NHX = SX(I) AND NHY = SY(I) THEN GAME = 0 : GOTO 2000
1100 NEXT I
1110 REM ==== MOVE BODY ====
1120 TX = SX(SLEN - 1)
1130 TY = SY(SLEN - 1)
1140 FOR I = SLEN - 1 TO 1 STEP -1
1150 SX(I) = SX(I - 1)
1160 SY(I) = SY(I - 1)
1170 NEXT I
1180 SX(0) = NHX
1190 SY(0) = NHY
1200 REM ==== FOOD CELL CHECK ====
1210 EAT = 0
1220 IF SX(0) = FX AND SY(0) = FY THEN EAT = 1
1230 IF EAT = 1 THEN SLEN = SLEN + 1
1240 IF EAT = 1 THEN SX(SLEN - 1) = TX
1250 IF EAT = 1 THEN SY(SLEN - 1) = TY
1260 IF EAT = 1 THEN SCORE = SCORE + 10
1270 IF EAT = 1 THEN GOSUB 9200
1280 IF EAT = 1 THEN GOSUB 9000
1290 REM ==== UPDATE ALL SNAKE SPRITES ====
1300 FOR I = 0 TO MAXLEN - 1
1310 ID = I + 1
1320 IF I < SLEN THEN XPIX = OFFX + SX(I) * CELL ELSE XPIX = -CELL
1330 IF I < SLEN THEN YPIX = OFFY + SY(I) * CELL ELSE YPIX = -CELL
1340 ILI SPRITE MOVE ID XPIX YPIX
1350 ILI SPRITE DRAW ID
1360 NEXT I
1370 REM ==== SEGMENTS BEYOND SLEN ARE KEPT OFF-SCREEN ====
1380 REM ==== FIXED SPEED ====
1390 DELAY SPEED
1400 GOTO 880
2000 REM ===== GAME OVER =====
2010 REM ==== CENTERED POSITION CALCULATION WITHIN THE PLAY AREA ====
2020 GW = GRIDW * CELL
2021 GH = GRIDH * CELL
2022 GX = OFFX + (GW / 2) - (LEN("GAME OVER") * 18 / 2)
2023 GY = OFFY + (GH / 2) - 12
2024 ILI TEXT GX GY 3 "GAME OVER" 255 0 0
2030 GAME = 0
2031 DELAY 3000
2032 GOTO 35
2040 END
9000 REM ==== PLACE FOOD ====
9010 FOK = 0
9020 FX = RND(0, GRIDW)
9030 FY = RND(0, GRIDH)
9040 REM ==== AVOID CELLS OCCUPIED BY THE SNAKE ====
9050 FOR I = 0 TO SLEN - 1
9060 IF FX = SX(I) AND FY = SY(I) THEN FOK = 1
9070 NEXT I
9080 IF FOK = 1 THEN GOTO 9020
9090 XPIX = OFFX + FX * CELL
9100 YPIX = OFFY + FY * CELL
9110 IF FOODNEW = 1 THEN ILI SPRITE NEW 200 RECT XPIX YPIX CELL CELL 255 0 0 ELSE ILI SPRITE MOVE 200 XPIX YPIX
9120 FOODNEW = 0
9130 ILI SPRITE DRAW 200
9140 RETURN
9200 REM ===== AGGIORNA SCORE HUD (SPRITE NUM ID_SCORE) =====
9210 ILI SPRITE SETNUM ID_SCORE SCORE 255 255 0
9220 ILI SPRITE DRAW ID_SCORE
9230 RETURN
