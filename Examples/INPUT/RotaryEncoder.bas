10   CLK = 15
20   DT = 12
30   SW = 33
40   PINMODE CLK INPUT PULLUP
50   PINMODE DT INPUT PULLUP
60   PINMODE SW INPUT PULLUP
70   POS=0
80   OLD=DREAD(CLK)
90   REM ==== MAIN LOOP ====
100   A=DREAD(CLK)
110   IF A<>OLD THEN GOTO 150
120   REM ==== BUTTON CONTROL ====
130   IF DREAD(SW)=0 THEN GOTO 310
140   GOTO 100
150   IF A=0 THEN GOTO 180
160   OLD=A
170   GOTO 100
180   B=DREAD(DT)
190   IF B=1 THEN GOTO 230
200   POS=POS-1
210   PRINT "CCW"
220   GOTO 250
230   POS=POS+1
240   PRINT "CW"
250   REM ==== WAIT FOR ROTATION RELEASE ====
260   A=DREAD(CLK)
270   IF A=1 THEN GOTO 290
280   GOTO 260
290   OLD=A
300   GOTO 100
310   REM ==== BUTTON PRESSED ====
320   PRINT "SW"
330   REM ==== WAIT FOR BUTTON RELEASE ====
340   IF DREAD(SW)=1 THEN GOTO 360
350   GOTO 340
360   GOTO 100
