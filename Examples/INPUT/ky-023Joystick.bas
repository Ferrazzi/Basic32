5  REM ===== KY-023 JOYSTICK TEST =====
10  XPIN = 34
20  YPIN = 35
30  SWPIN = 15
40  PINMODE SWPIN INPUT PULLUP
50  ADCMAX = 4095
60  MID = ADCMAX / 2
70  DEAD = 200 REM ZONA MORTA
80  REM ===== LOOP =====
90  X = AREAD(XPIN)
100  Y = AREAD(YPIN)
110  S = DREAD(SWPIN)
120  REM ==== X DIRECTION ====
130  DX$ = "CENTER"
140  IF X > MID + DEAD THEN DX$ = "RIGHT"
150  IF X < MID - DEAD THEN DX$ = "LEFT"
160  REM ==== Y DIRECTION ====
170  DY$ = "CENTER"
180  IF Y > MID + DEAD THEN DY$ = "DOWN"
190  IF Y < MID - DEAD THEN DY$ = "UP"
200  REM ==== BUTTON ====
210  IF S = 0 THEN B$ = "PRESSED" ELSE B$ = "RELEASED"
220  PRINT "X=";X;"  Y=";Y;"  DIR=";DX$;",";DY$;"  SW=";B$
230  DELAY 200
240  GOTO 90
