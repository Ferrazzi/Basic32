10 REM ===== MANDELBROT OLED 128X64 =====
20 OLED INIT 128 64 3C
30 OLED CLEAR
40 W = 128 : H = 64
41 W1 = W - 1 : H1 = H - 1
50 MAXITER = 32
60 REM ---- PIXEL -> COMPLEX PLANE ----
70 XMIN = -2.2 : XMAX = 1.2
80 YMIN = -1.2 : YMAX = 1.2
90 DX = (XMAX - XMIN) / W
100 DY = (YMAX - YMIN) / H
110 REM OPTIONAL: TITLE (BUFFERED)
150 OLED CLEAR
160 REM ===== ROW LOOP =====
170 FOR PY = 0 TO H1
180 CI = YMAX - PY * DY
190 REM ===== COLUMN LOOP =====
200 FOR PX = 0 TO W1
210 CR = XMIN + PX * DX
220 ZR = 0 : ZI = 0 : I = 0
230 REM ===== ITERATIONS =====
240 IF ZR*ZR + ZI*ZI > 4 THEN GOTO 280
250 ZT = ZR*ZR - ZI*ZI + CR
260 ZI = 2*ZR*ZI + CI
270 ZR = ZT : I = I + 1 : IF I < MAXITER THEN GOTO 240
280 REM ===== INSIDE SET: WHITE / OUTSIDE: BLACK =====
290 IF I = MAXITER THEN OLED PIXEL PX PY WHITE 0 ELSE OLED PIXEL PX PY BLACK 0
300 NEXT PX
310 REM END OF ROW: UPDATE DISPLAY (EVERY 4 ROWS FOR SPEED)
320 IF (PY MOD 4)=0 THEN OLED DRAW ELSE A=0
330 NEXT PY
340 OLED DRAW
370 END
